apply plugin: 'com.android.application'

repositories {
    maven { url 'http://oss.jfrog.org/artifactory/oss-snapshot-local/' }
}

def getProps(path) {
    Properties props = new Properties()
    props.load(new FileInputStream(file(path)))
    return props
}

android {
    compileSdkVersion sdk.compile

    defaultConfig {
        applicationId "home.oleg.placesnearme"
        minSdkVersion sdk.min
        targetSdkVersion sdk.target
        versionCode 1
        versionName "1.0"

        getProps('./config/config.props').each { p ->
            buildConfigField 'String', p.key, p.value
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility javaVersion
        targetCompatibility javaVersion
    }

}

dependencies {
    implementation project(':network')
    implementation project(':core-presentation')
    implementation project(':core-di')

    implementation project(':data')
    implementation project(':domain')
    implementation project(':common')

    implementation project(':feature-map')
    implementation project(':feature-main')
    implementation project(':feature-venue-detail')
    implementation project(':feature-favorite-venues')
    implementation project(':feature-add-favorite')

    implementation deps.support.supportV13
    implementation deps.lifecycle.extensions

    implementation deps.dagger.runtime
    annotationProcessor deps.dagger.compiler

    implementation deps.timber
}
